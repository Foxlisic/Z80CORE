
; ----------------------------------------------------------------------
; Вычисление точки для выдачи текста
; ----------------------------------------------------------------------
; INPUT:    B(y=0..23), C(x=0..31)
; OUTPUT:   HL
; ----------------------------------------------------------------------

GetCharAddrByXY:

        push    af
        ld      a, c
        and     $1f
        ld      l, a        ; L = X & 1F
        ld      a, b
        and     $07
        rrca
        rrca
        rrca
        or      l
        ld      l, a        ; L |= (Y & 7)<<5
        ld      a, b
        and     $18
        or      $40
        ld      h, a        ; H |= (Y & 0x18) | 0x40
        pop     af
        ret

; ----------------------------------------------------------------------
; Печать символа A по адресу HL
; ----------------------------------------------------------------------
; INPUT:    A (char), HL (addr)
; AFFECT:   A
; ----------------------------------------------------------------------

PrintCharAt:

        push    bc          ; Сохраним BC,DE,HL
        push    de
        push    hl
        ex      de, hl      ; Временно сохраним HL -> DE
        sub     a, $20      ; Вычесть непечатаемые 32 символа
        ld      h, 0
        ld      l, a        ; HL=A
        add     hl, hl
        add     hl, hl
        add     hl, hl      ; HL=8*HL
        ld      bc, $3d00   ; Источник данных для шрифта
        add     hl, bc
        ex      de, hl      ; Теперь DE = $3d00 + 8*A, HL вернули
        ld      b, 8        ; 8 строк
PCALp:  ld      a, (de)     ; Загрузка данных для рисования
        ld      (hl), a     ; Нарисовать
        inc     h           ; Символ вниз 
        inc     de          ; Следующий байт данных
        djnz    PCALp       ; Повторять 8 раз
        pop     hl          ; Извлечь HL,DE,BC
        pop     de
        pop     bc
        ret

; ----------------------------------------------------------------------
; Печать строки на экране с учетом сдвига
; ----------------------------------------------------------------------
; INPUT:  B(y), C(x) - указатель, где печатать
;         DE - адрес строки (8-бит терминированная)
; AFFECT: A, BC
;         DE - конец строки, HL - последняя позиция курсора
; ----------------------------------------------------------------------

PrintString:

        call    GetCharAddrByXY     ; Вычислить позицию
PS1:    ld      a, (de)             ; Извлечь символ
        and     a                   ; Проверить на 0
        ret     z                   ; Если 0, то завершить
        call    PrintCharAt         ; Печать символа
        inc     de                  ; К следующему
        inc     l                   ; X += 1
        jr      nz, PS1             ; Если не вышел за пределы еще
        ld      a, h
        add     $08                 ; H += 8 Следующая секция
        ld      h, a
        cp      $58                 ; Сравнить Y с 24 позицией
        jr      c, PS1              ; Повторять, пока Y < 24
        ld      hl, $50E0           ; Позиция X,Y=(0,23)
        call    ScrollUp
        jr      PS1

; ----------------------------------------------------------------------
; Скроллинг экрана вверх
; ----------------------------------------------------------------------

ScrollUp:

        ld      de, $4100
        ld      hl, $4000
        ld      bc, $0800

        ld      a, (de)
        ld      (hl), a
        inc     de
        inc     hl
        ret

; ----------------------------------------------------------------------
; Очистка экрана в определенный цвет
; ----------------------------------------------------------------------
; INPUT: A - Атрибут очистки
; ----------------------------------------------------------------------

ClearScreen:

        ld      hl, $5800
        ld      (hl), a
        ld      de, $5801
        ld      bc, $02FF
        ldir
        rrca
        rrca
        rrca
        and     $07
        out     ($fe), a        ; Установить также бордюр
        xor     a
        ld      hl, $4000
        ld      bc, $17FF
        ld      de, $4001
        ld      (hl), a
        ldir
        ret
