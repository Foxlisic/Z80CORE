1000 DIM R(55): DIM C(55)
1010 LET FANFARE=2000: LET WORM=3000: LET KEY=4000: LET GOBBLE=5000: LET STATUS=6000: LET TARGET=7000
1020 BORDER 1 :PAPER 7:INK 0
1030 LET HSC=0
1040 LET SCORE=0: LET WORMS=3: LET LEVEL=1
1050 LET DEAD=0: LET WON=0: LET C=16: LET R=10: LET P=1: LET S=5: LET CMOVE=0:LET RMOVE=1: LET H$="*"
1060 FOR I=1 TO 55: LET R(I)=-1: NEXT I
1070 CLS: PRINT AT 0,0; PAPER 6: PRINT AT 21,0; PAPER 6
1080 GO SUB STATUS
1090 GO SUB TARGET
1100 GO SUB KEY
1110 GO SUB WORM
1120 IF NOT DEAD AND NOT WON THEN GO TO 1100
1130 IF WON THEN LET LEVEL=LEVEL+1: GO SUB FANFARE: GO TO 1050
1140 IF DEAD THEN FOR I=1 TO 20: BEEP 0.05,15: NEXT I
1160 FOR I=1 TO S: BEEP 0.01,0: PRINT AT R(P),C(P);PAPER 7;" "
1170 LET P=P+1: IF P>S THEN LET P=1
1180 NEXT I
1190 PRINT AT Y,X;PAPER 7;" ": GO SUB STATUS
1200 LET I$=CHR$ 18+CHR$ 1+"PRESS ENTER TO START"+"GAME"+CHR$ 18+CHR$ 0
1210 INPUT (I$+" ");LINE A$
1220 GO TO 1040
2010 DATA 0.06,18,0.06,19,0.06,21,0.15,27,0.06,21,0.20,27
2020 RESTORE FANFARE: FOR I=1 TO 6: READ L,T:BEEP L,T: NEXT I
2030 RETURN
3010 IF R(P)<>-1 THEN PRINT AT R(P),C(P); PAPER 7;" "
3020 LET R=R+RMOVE: LET C=C+CMOVE
3030 IF R>20 OR R<1 OR C>31 OR C<0 THEN LET DEAD=1: RETURN
3040 IF ATTR (R,C)=16 THEN LET DEAD=1: RETURN
3050 LET R(P)=R: LET C(P)=C
3060 IF SCREEN$ (R,C)="$" THEN GO SUB GOBBLE
3070 PRINT AT R(P),C(P); PAPER 2;H$
3080 LET P=P+1: IF P>S THEN LET P=1
3090 RETURN
4010 LET A$=INKEY$ : IF A$="" THEN RETURN
4020 IF CODE A$>96 THEN LET A$=CHR$ (CODE A$-32)
4030 IF A$="W" AND CMOVE THEN LET RMOVE=-1: LET CMOVE=0: RETURN
4040 IF A$="S" AND CMOVE THEN LET RMOVE=1: LET CMOVE=0: RETURN
4050 IF A$="A" AND RMOVE THEN LET RMOVE=0: LET CMOVE=-1: RETURN
4060 IF A$="D" AND RMOVE THEN LET RMOVE=0: LET CMOVE=1: RETURN
4070 RETURN
5010 FOR I=2 TO 4 STEP 0.5
5020 BEEP 0.01,EXP I-10: NEXT I
5030 LET SCORE=SCORE+1: GO SUB STATUS
5040 LET S=S+5: IF S=55 THEN LET WON=1: RETURN
5050 GO SUB TARGET
5060 RETURN
6020 PRINT AT 0,0;PAPER 6; " SCORE ";SCORE," HI-SCORE ";HSC
6030 PRINT AT 21,0;PAPER 6;" LEVEL ";LEVEL," WORMS ";WORMS
6040 RETURN
7010 LET X=RND*31: LET Y=RND*19+1
7020 IF X=C AND Y=R THEN GO TO TARGET
7030 IF ATTR (Y,X)=16 THEN GO TO TARGET
7040 PRINT AT Y,X; PAPER 4;"$"
7050 RETURN
